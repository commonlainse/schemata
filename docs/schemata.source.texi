\input texinfo   @c -*-texinfo-*-
@comment $Id@w{$}
@comment %**start of header
@settitle SCHEMATA
@syncodeindex pg cp
@setfilename schemata.info
@include common-lisp.texi

@copying

Copyright @copyright{} 2021 Mariano Montone

@end copying


@dircategory Common Lisp
@direntry
* schemata: (schemata) Schema validation and serialization library for Common Lisp.
@end direntry

@titlepage
@title SCHEMATA

@author Mariano Montone ( @email{marianomontone@@gmail.com} )
@page
@vskip 0pt plus 1filll
@insertcopying
@end titlepage

@contents

@node Top
@top Top
@cindex schemata

@menu
* Intro::
* Installation::
* Usage::
* Schema types::
* Data generation::
* Reference::
* Index::
@end menu

@node Intro
@chapter Introduction
@anchor{introduction}

Generic purpose schema library for serialization and validation of data.

This library is used by CL-REST-SERVER for API serialization and validation.

It features:

@itemize @bullet
@item Validation using schemas.
@item Serialization and unserialization using schemas.
@item Generation of (random) data from schemas. (incomplete implementation atm)
@item JSON-Schema parsing (incomplete implementation atm).
@item Integration with Common Lisp type system.
@item A schema class metaclass.
@end itemize

@node Installation
@chapter Installation
@anchor{installation}


With Quicklisp:

@lisp
(ql:quickload "schemata")
@end lisp

@node Usage
@chapter Usage
@anchor{usage}

@section Schema definition

Use (@clref schemata:defschema macro) for defining schemas.

Schema example:

@lisp
(schemata:defschema customer
    (object "customer"
             ((id string :external-name "id" :accessor
                  customer-id :documentation "customer id")
              (number string :external-name "number" :required nil
                              :accessor customer-nr :documentation
                      "customer number")
              (name string :external-name "name" :accessor
                    customer-name :documentation "customer name")
              (address-1 string :external-name "address1"
                                 :required nil :documentation
                         "customer first address")
              (address-2 string :external-name "address2"
                                 :required nil :documentation
                         "customer second address")
              (postal-code string :external-name "postalcode"
                                   :required nil :documentation
                           "postal code")
              (postal-area string :external-name "postalarea"
                                   :required nil :documentation
                           "postal area")
              (country string :external-name "country" :required nil 
                       :documentation "country code")
              (phone string :external-name "phone" :required nil
                             :documentation "phone")
              (fax string :external-name "fax" :required nil
                           :documentation "fax")
              (email string :external-name "email" :required nil
                             :documentation "email"))
             (:documentation "customer data fetched")))
@end lisp

@section Validation using schemas

Use (@clref schemata:validate-with-schema function).

@section Serialization using schemas

Via @emph{generic-serializer} library.

Use (@clref schemata:serialize-with-schema function).

@lisp
(with-output-to-string (s)
    (gs:with-serializer-output s
        (gs:with-serializer :json
           (serialize-with-schema *schema* user))))
@end lisp

@lisp
(with-output-to-string (s)
  (gs:with-serializer-output s
    (gs:with-serializer :json
      (serialize-with-schema
       (find-schema 'user-schema) *user*))))
@end lisp

@section Unserialization using schemas

Use (@clref schemata:unserialize-with-schema function).

@lisp
(unserialize-with-schema
   (find-schema 'user-schema)
   (json:decode-json-from-string data)
   :json)
@end lisp

@section Patch and updates

See (@clref schemata:patch-with-schema function) and (@clref schemata:populate-with-schema function).

@node Schema types
@chapter Schema types
@section Type schemas

Schemas can be built from Common Lisp types:

@verbatim
SCHEMATA> (defparameter *s* (schema string))
*S*
SCHEMATA> *s*
#<TYPE-SCHEMA STRING {1006FBBD13}>
SCHEMATA> (validate-with-schema *s* "22")
NIL
SCHEMATA> (validate-with-schema *s* 22 :error-p nil)
#<VALIDATION-ERROR "~s is not of type: ~a" {100152EB13}>
@end verbatim

@section Object schema

Object schemas are built using the syntax: @code{(object name attributes options)}.
Attributes are specified as: @code{(attribute-name attribute-type &rest options)}.

The @code{attribute-type} is parsed as a schema.

Possible attribute options are: `required`, `required-message`, `default`, `accessor`, `writer`, `reader`, `parser`, `validator`, `add-validator`, `formatter`, `external-name`, `serializer`, `unserializer`, `slot`.

Example:

@verbatim
SCHEMATA> (schema (object person
                          ((name string)
                           (age integer :required nil))))
#<OBJECT-SCHEMA {1001843543}>
@end verbatim

@section List schemas

@subsection list-of

Schema for lists with elements of certain type/schema.

Syntax: @code{(list-of list-element-schema)}

Examples:

@lisp
(schema (list-of string))
@end lisp

@lisp
(schema (list-of (or string number)))
@end lisp

@verbatim
SCHEMATA> (defparameter *s* (schema (list-of integer)))
*S*
SCHEMATA> (validate-with-schema *s* '(1 2 "foo"))
; Evaluation aborted on #<SCHEMATA:VALIDATION-ERROR "~s is not of type: ~a" {1006ECA323}>.
SCHEMATA> (validate-with-schema *s* '(1 2 3))
NIL
@end verbatim

@subsection list

Schema for list with all its elements specified with schemas.

Syntax: @code{(list &rest schemas)}

Examples:

@lisp
(schema (list string number boolean))
@end lisp

@subsection alist-of

Schema for association lists with certain type of keys and values.

Syntax: @code{(alist-of (key-schema . value-schema))}

Examples:

@lisp
(schema (alist-of (keyword . string)))
@end lisp

@subsection alist

Schema for association lists with certain keys and values.

Syntax: @code{(alist association-list &rest options)}

where association-list is a list of conses with key and schema.

Options can be @code{:required}, @code{:optional} and @code{:allow-other-keys}.

Examples:

@lisp
(schema (alist ((:x . string)(:y . number))))
@end lisp

@lisp
(schema (alist ((:x . string)(:y . number)) :optional (:y)))
@end lisp

@subsection plist-of

Schema for property lists with certain type of keys and values.

Syntax: @code{(plist-of key-schema value-schema)}

Examples:

@lisp
(schema (plist-of keyword string))
@end lisp

@subsection plist

Schema for property lists with certain keys and values.

Syntax: @code{(plist property-list &rest options)}

where property-list specifies the schemas for the keys.

Options can be @code{:required}, @code{:optional} and @code{:allow-other-keys}.

Examples:

@lisp
(schema (plist (:x string :y number)))
@end lisp

@lisp
(schema (plist (:x string :y number) :optional (:y)))
@end lisp

@section HASH-TABLE schemas

@subsection hash-table-of

Schema for hash-tables with certain type of keys and values.

Syntax: (hash-table-of key-schema value-schema)

Examples:

@lisp
(schema (hash-table-of keyword string))
@end lisp

@subsection hash-table

Schema for property lists with certain keys and values.

Syntax: (hash-table property-list &rest options)

where property-list specifies the schemas for the keys and values.

Options can be :required, :optional and :allow-other-keys, and other initargs that are passed to MAKE-HASH-TABLE.

Examples:

@lisp
(schema (hash-table (:x string :y number)))
@end lisp

@lisp
(schema (hash-table (:x string :y number) :optional (:y)))
@end lisp

@section CONS schema

Schema for CONSes.

Syntax: (cons car-schema cdr-schema)

Examples:

@lisp
(schema (cons symbol string))
@end lisp
    
@section AND and OR schemas

@subsection or

Schemas disjunction.

Syntax: (or &rest schemas)

Example:

@lisp
(schema (or string symbol))
@end lisp

@subsection and

Schemas conjunction.

Syntax: (and &rest schemas)

Example:

@lisp
(schema (and integer (satisfies evenp)))
@end lisp

@section Schema references

Defined schemas can be referenced via either `(schema schema-name)` or `(ref schema-name)` (they are identical).

Example:

@verbatim
SCHEMATA> (defschema person
            (object person
                    ((name string))))
#<OBJECT-SCHEMA {1006F8A813}>
SCHEMATA> (defparameter *list-of-person* (schema (list-of (ref person))))
*LIST-OF-PERSON*
SCHEMATA> *list-of-person*
#<LIST-SCHEMA {1006F8C2A3}>
SCHEMATA> (parse-with-schema *list-of-person* '((("name" . "Mariano")) (("name" . "Peter"))))
(((NAME . "Mariano")) ((NAME . "Peter")))
SCHEMATA> (validate-with-schema *list-of-person* '((("name" . 22)) (("name" . "Peter"))))
; processing (DEFMETHOD SCHEMA-VALIDATE ...); Evaluation aborted on #<SB-PCL::NO-APPLICABLE-METHOD-ERROR {1008018513}>.
SCHEMATA> (validate-with-schema *list-of-person* '((("name" . 22)) (("name" . "Peter"))))
; Evaluation aborted on #<SCHEMATA:VALIDATION-ERROR "~s is not of type: ~a" {10082EB883}>.
SCHEMATA> (validate-with-schema *list-of-person* '((("name" . "Mariano")) (("name" . "Peter"))))
NIL
SCHEMATA> (validate-with-schema *list-of-person* '((("names" . "Mariano")) (("name" . "Peter"))))
; Evaluation aborted on #<SCHEMATA:VALIDATION-ERROR "Attributes not part of schema: ~a" {1008CD3DD3}>.
@end verbatim

@chapter SATISFIES-SCHEMA type

Schemata integrates with the Lisp type system via the SATISFIES-SCHEMA type.
Schemas can be thought as types over data.
Defined schemas can be checked using TYPEP and CHECK-TYPE with the type `(satisfies-schema schema-name)`.

Example:

@verbatim
SCHEMATA> (defschema string-schema string)
#<TYPE-SCHEMA STRING {10019DA8B3}>
SCHEMATA> (typep "foo" '(satisfies-schema string-schema))
T
SCHEMATA> (typep 22 '(satisfies-schema string-schema))
NIL
SCHEMATA> (let ((x "foo"))
            (check-type x (satisfies-schema string-schema))
            x)
"foo"
@end verbatim

@chapter SCHEMA-CLASS metaclass

SCHEMA-CLASS classes get an schema attached.

Example:

@verbatim
SCHEMATA> (def-schema-class person ()
            ((name :type string :initarg :name)
             (age :type integer :required nil :initarg :age)))
#<SCHEMA-CLASS SCHEMATA::PERSON>

SCHEMATA> (validate-with-schema (find-class 'person) '(("name" . "Mariano") ("age" . 22)))
NIL

SCHEMATA> (validate-with-schema (find-class 'person) '(("name" . "Mariano") ("age" . 'asdf)) :error-p nil)
#<VALIDATION-ERROR 'ASDF is not of type: INTEGER {100109F833}>

SCHEMATA> (generic-serializer:with-serializer :json
            (generic-serializer:serialize (make-instance 'person :name "Mariano" :age 44)))
{"name":"Mariano","age":44}
@end verbatim

@node Data generation
@chapter Data generation

Schemata can generate random data from schemas. It uses @code{check-it} library generators for that.

Load @code{schemata-generators} system.

Then call @code{check-it:generate} with a schema object.

Example:

@lisp
(defschema person
    (object person
            ((name string)
             (age integer :required nil)
             (friend (ref person) :required nil))))
@end lisp

@lisp
(generate (find-schema 'person))
@end lisp

can generate:

@example
((NAME . "21p7E0w8")
 (FRIEND (NAME . "hD39Dwo")
  (FRIEND (NAME . "QFC67xg206") (AGE . 4)
   (FRIEND (NAME . "bRtUL1z51")
    (FRIEND (NAME . "0")
     (FRIEND (NAME . "ddB57idmh32C4T") (AGE . 1)
      (FRIEND (NAME . "eNKzc") (AGE . 8))))))))
@end example

For more control over the generation, attach a generator to schemas via @code{:generator} initarg.

@node Reference
@chapter Reference
@anchor{reference}

@menu
* SCHEMATA package::
@end menu

@node SCHEMATA package
@section SCHEMATA package
(@clpackage :schemata)

@node Index
@chapter Index

@printindex cp
@printindex vr
@printindex fn

@bye
